plugins {
    id 'java-library'
    id 'eclipse'
    id 'antlr'
    id 'org.inferred.processors' version '2.1.0'
}

configurations {
	autogen
	api.extendsFrom autogen
}

sourceSets {
    main {
        java {
            srcDirs += ["build/generated-src/antlr/main"]
        }
    }
}

generateGrammarSource {
	outputDirectory = file("build/generated-src/antlr/main/org/immersed/gaffe")
    arguments += ["-package", "org.immersed.gaffe"]
}

eclipse {
  jdt {
    file {
      withProperties { properties ->
      	sourceSets.main.resources.srcDirs.each{ 
      		def profiles = new XmlParser().parse("$it/formatter.xml")
      		def profile = profiles.find { it.@name == 'gaffe' }
      		
      		profile.each { setting ->
      			properties.put(setting.@id, setting.@value);
      		}
      	}
      }
    }
  }
}

dependencies {
	processor 'org.inferred:freebuilder:1.15.0'
	antlr 'org.antlr:antlr4:4.7.2'

	implementation 'org.mockito:mockito-core:2.23.4'

	implementation 'org.eclipse.jdt:org.eclipse.jdt.core:3.21.0'

	implementation 'org.slf4j:slf4j-simple:1.7.25'
	implementation "org.gradle:gradle-tooling-api:4.8"

	implementation 'io.github.classgraph:classgraph:4.6.10'
	implementation 'com.squareup:javapoet:1.11.1'
}